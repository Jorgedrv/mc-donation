plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "4.4.1.3373"
	id 'jacoco'
}

group = 'com.tn.donation'
version = '0.0.1-SNAPSHOT'
description = 'Donation Microservice for Practicing'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	runtimeOnly 'org.postgresql:postgresql'
}

sonarqube {
	properties {
		if (project.hasProperty("local")) {
			file("sonar-local.properties").withInputStream {
				def props = new Properties()
				props.load(it)
				props.forEach { key, value ->
					property key.toString(), value.toString()
				}
			}
		} else {
			property "sonar.projectKey", "Jorgedrv_mc-donation"
			property "sonar.organization", "jorgedrv"
			property "sonar.host.url", "https://sonarcloud.io"

			property "sonar.projectName", "mc-donation"
			property "sonar.projectVersion", "1.0"
			property "sonar.sourceEncoding", "UTF-8"

			property "sonar.sources", "src/main/java"
			property "sonar.tests", "src/test/java"
			property "sonar.java.binaries", "build/classes/java/main"

			property "sonar.coverage.jacoco.xmlReportPaths",
					"build/reports/jacoco/test/jacocoTestReport.xml"

			property "sonar.exclusions", """
**/McDonationApplication.java,
**/infrastructure/repository/jpa/entity/**,
**/common/handler/**,
**/common/exception/ErrorResponse.java,
**/infrastructure/initializer/**
""".stripIndent()

			property "sonar.branch.name", "master"
		}
	}
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		xml.outputLocation = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect { dir ->
			fileTree(dir: dir, exclude: [
					'**/McDonationApplication.java',
					'**/infrastructure/repository/jpa/entity/**',
					'**/common/handler/**',
					'**/common/exception/ErrorResponse.java',
					'**/infrastructure/initializer/**'
			])
		}))
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
